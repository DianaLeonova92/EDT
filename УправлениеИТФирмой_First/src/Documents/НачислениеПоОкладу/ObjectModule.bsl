
Процедура ОбработкаПроведения(Отказ, Режим)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Оклад КАК Оклад,
	|	НачислениеПоОкладуСотрудники.Сотрудник КАК Сотрудник
	|ИЗ
	|	Документ.НачислениеПоОкладу.Сотрудники КАК НачислениеПоОкладуСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО НачислениеПоОкладуСотрудники.Сотрудник = Сотрудники.Ссылка
	|ГДЕ
	|	НачислениеПоОкладуСотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ОкладыСотрудников = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		ОкладыСотрудников.Вставить(Выборка.Сотрудник, Выборка.Оклад);
	КонецЦикла;
	
	// регистр Зарплата
	Движения.Зарплата.Очистить();
	
	Для Каждого ТекСтрокаСотрудники Из Сотрудники Цикл
		Движение = Движения.Зарплата.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.НачисленияУдержания.ОплатаПоОкладу;
		Движение.ПериодДействияНачало = НачалоМесяца(Дата);
		Движение.ПериодДействияКонец = КонецМесяца(Дата);
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = ТекСтрокаСотрудники.Сотрудник;
	КонецЦикла;
	
	Движения.Зарплата.Записать(); 
	
	Для Каждого Движение Из Движения.Зарплата Цикл
		
		Если Движение.ВидРасчета <> ПланыВидовРасчета.НачисленияУдержания.ОплатаПоОкладу Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор = Новый Структура("Регистратор, Сотрудник", Ссылка,Движение.Сотрудник);
		Норма = РегистрыРасчета.Зарплата.ПолучитьДанныеГрафика(Отбор,
		ВидПериодаРегистраРасчета.ПериодДействия)[0].РабочийДень;
		
		Факт = РегистрыРасчета.Зарплата.ПолучитьДанныеГрафика(Отбор,
		ВидПериодаРегистраРасчета.ФактическийПериодДействия)[0].РабочийДень;
		
		Движение.Сумма = ?(Норма = 0, 0 , ОкладыСотрудников.Получить(Движение.Сотрудник) * Факт / Норма);
		//Результат = Движение.Сумма * Факт / Норма;
		//Движение.Сумма = Результат;
		
	КонецЦикла;
	
	Движения.Зарплата.Записать(, Истина);

КонецПроцедуры
