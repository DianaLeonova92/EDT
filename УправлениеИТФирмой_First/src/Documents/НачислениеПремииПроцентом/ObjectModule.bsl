
Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр Зарплата
	Движения.Зарплата.Очистить();
	
	Для Каждого ТекСтрокаСотрудники Из Сотрудники Цикл
		Движение = Движения.Зарплата.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.НачисленияУдержания.ПремияПроцентом;
		Движение.ПериодДействияНачало = НачалоМесяца(Дата);
		Движение.ПериодДействияКонец = КонецМесяца(Дата);
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = ТекСтрокаСотрудники.Сотрудник;
	КонецЦикла;
	
	Движения.Зарплата.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗарплатаБазаЗарплата.Сотрудник КАК Сотрудник,
		|	ЗарплатаБазаЗарплата.СуммаБаза КАК СуммаБаза,
		|   ЗарплатаБазаЗарплата.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	РегистрРасчета.Зарплата.БазаЗарплата(
		|			&Измерения,
		|			&Измерения,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета = &ВидРасчета) КАК ЗарплатаБазаЗарплата";
	
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.НачисленияУдержания.ПремияПроцентом);
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	ВыборкаБаза = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаБаза.Следующий() Цикл
		
		Движение = Движения.Зарплата[ВыборкаБаза.НомерСтроки - 1];
		Движение.Сумма = ВыборкаБаза.СуммаБаза * ПроцентПремии / 100;
		
	КонецЦикла; 
	
	Движения.Зарплата.Записать(, Истина);

КонецПроцедуры
