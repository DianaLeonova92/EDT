
&НаКлиенте
Асинх Процедура Загрузить(Команда)
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	Диалог = Новый ДиалогВыбораФайла(Режим);
	Фильтр = НСтр(("ru = 'Файл CSV'; en = 'File CSV'")
	+ "(*.csv)|*.csv");
	Диалог.Фильтр = Фильтр;
	Диалог.Заголовок = "Выберите файл";
	
	ВыбранныеФайлы = Ждать Диалог.ВыбратьАсинх();
	
	Если ВыбранныеФайлы = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДанныеФайла = Ждать ПрочитатьФайл(ВыбранныеФайлы[0]);
	
	РезультатПреобразованияДанныхФайла = РезультатПреобразованияДанныхФайла(ДанныеФайла); 
	
	Если Не РезультатПреобразованияДанныхФайла.ЕстьНайденнаяНоменклатура Тогда
		
		Сообщить("В файле нет номенклатуры из базы данных. Загрузка отменена.");
		
		Возврат;
		
	ИначеЕсли РезультатПреобразованияДанныхФайла.ЕстьНеНайденнаяНоменклатура Тогда
		
		РезультатВопроса = Ждать ВопросАсинх("Не все номенклатурные позиции из файла существуют в справочнике. Продолжить создание документа?", РежимДиалогаВопрос.ДаНет);
		
		Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СоздатьДокумент(РезультатПреобразованияДанныхФайла.Данные);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РезультатПреобразованияДанныхФайла(ДанныеФайла)

	Результат = Новый Структура("ЕстьНеНайденнаяНоменклатура, ЕстьНайденнаяНоменклатура, Данные", Ложь, Ложь, ДанныеФайла);
	
	Для Каждого Данные Из Результат.Данные Цикл
		
		Данные.Вставить("Номенклатура", Справочники.Номенклатура.НайтиПоНаименованию(Данные.НаименованиеНоменклатуры)); 
		
		Если Не ЗначениеЗаполнено(Данные.Номенклатура) Тогда
			
			Результат.ЕстьНеНайденнаяНоменклатура = Истина;
			
			ШаблонСообщения = НСтр("ru = 'Номенклатура: %1 не найдена'");
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(ШаблонСообщения, Данные.НаименованиеНоменклатуры);
			Сообщение.Сообщить();
			
		Иначе
			
			Результат.ЕстьНайденнаяНоменклатура = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат; 

КонецФункции

&НаКлиенте
Асинх Функция ПрочитатьФайл(ПутьКФайлу)
	
	ТекстовыйФайлЗагрузки = Новый ТекстовыйДокумент;
	Разделитель = ";";
	
	Ждать ТекстовыйФайлЗагрузки.ПрочитатьАсинх(ПутьКФайлу, КодировкаТекста.UTF8);
	
	Результат = Новый Массив; 
	
	Для НомерСтроки  = 1 По ТекстовыйФайлЗагрузки.КоличествоСтрок() Цикл
		
		НоваяСтрока = ТекстовыйФайлЗагрузки.ПолучитьСтроку(НомерСтроки);
		
		Позиция = Найти(НоваяСтрока, ";");
		
		НаименованиеНоменклатуры = Сред(НоваяСтрока, 1, Позиция -1);
		Цена = Сред(НоваяСтрока, Позиция +1);
		
		ДанныеСтрокиДокумента = Новый Структура;
		ДанныеСтрокиДокумента.Вставить("НаименованиеНоменклатуры", НаименованиеНоменклатуры);
		ДанныеСтрокиДокумента.Вставить("Цена", Цена);
		
		Результат.Добавить(ДанныеСтрокиДокумента);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции   

&НаСервереБезКонтекста
Процедура СоздатьДокумент(ДанныеФайла)
	
	ДокументЦены = Документы.УстановкаЦен.СоздатьДокумент();
	ДокументЦены.Дата = ТекущаяДата();
	
	Для Каждого ДанныеСтроки Из ДанныеФайла Цикл
		
		//Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(ДанныеСтроки.НаименованиеНоменклатуры);
		
		Если Не ЗначениеЗаполнено(ДанныеСтроки.Номенклатура) Тогда 
			
			Продолжить;
			
		КонецЕсли;
		
		НоваяСтрокаТЧ = ДокументЦены.Цены.Добавить();
		НоваяСтрокаТЧ.Номенклатура = ДанныеСтроки.Номенклатура;
		НоваяСтрокаТЧ.Цена = ДанныеСтроки.Цена;
		
	КонецЦикла;
	
	ДокументЦены.Записать();
	
КонецПроцедуры

 